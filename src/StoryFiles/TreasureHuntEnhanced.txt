USE: <StackableObjects>, <WeightedObjects>;

[room]
{
    ID: <RUINSENTRANCE>;
    N: <COLUMNS>;
    S: <CIVILISATION>;
    SCOND: QUERY<isLocated(treasure,player,0)>;
    SBLOCKMESSAGE: "This is the direction you came into the ruins from.%nYou don't want to return to civilisation without claiming the ancient treasure you came for!";
    W: <ROCKYTUNNEL>;
    NAME: "Ruin's Entrance";
    DESC: "A newly excavated entrance way to long-lost underground ruins.%n%nBlinding sunlight streams into the network of caverns, illuminating the crumbling stone and marble structures inside.%n%nAhead of you are some stairs leading into a long-abandoned temple with cracked marble columns reaching%nto the roof of the caverns";
    DET: "the";
}

[room]
{
    ID: <CIVILISATION>;
    N: <RUINSENTRANCE>;
    NAME: "Civilisation";
    DESC: "The outside world as you know it.";
    DET: "";
}

[room]
{
    ID: <COLUMNS>;
    N: <ALTERROOM>;
    S: <RUINSENTRANCE>;
    E: <STORAGEROOM>;
    W: <EASTRIVER>;
    NAME: "Marble Column-way";
    DESC: "The ornate pathway up to the alter ahead of you is lined with tall marble columns.%nMany of these columns have cracked and crumbled over time.";
    DET: "the";
}

[object]
{
    ID: <SMALLBLOCK>;
    LOCATION: <COLUMNS>;
    NAME: "small block";
    DESC: "It's a small marble block. It looks as if it fallen from one of the ruined columns.%nThe block is still sturdy, perhaps it could be stacked up on something to climb somewhere unreachable...";
    SYNS: "little block", "smallest block", "little marble block", "small marble block", "block", "small", "broken column";
    PROPERTIES: _TAKEABLE, _STABLE;
    VALUES: (volume: 25), (surface: 25), (weight: 25);
}

[room]
{
    ID: <ALTERROOM>;
    S: <COLUMNS>;
    D: <TREASUREROOM>;
    DCOND: QUERY<weightOnObjectAtLeast(pressureplate1,50), weightOnObjectAtLeast(pressureplate2,50)>, _HIDDEN;
    DBLOCKMESSAGE: "You cannot move down.";
    NAME: "Grand Alter";
    DESC: "At the very end of the temple's long column-way stands a grand alter%nThe two pressure plates in front of it seem connected to it somehow.";
    ALTDESC: "The alter that once stood here has sunken into the floor, exposing a stairway emanating a soft golden glow...";
    CONDS: QUERY<weightOnObjectAtLeast(pressureplate1,50), weightOnObjectAtLeast(pressureplate2,50)>;
}

[object]
{
    ID: <ALTER>;
    LOCATION: <ALTERROOM>;
    NAME: "alter";
    DESC: "A grand marble alter rising from the centre of the room. Many ceremonies will have been performed here in ages past.";
    ALTDESC: "The alter is no longer visible as it has retreated into the ground and in its place is a stairway.";
    CONDS: QUERY<weightOnObjectAtLeast(pressureplate1,50), weightOnObjectAtLeast(pressureplate2,50)>;
    PROPERTIES: _STABLE;
    VALUES: (surface: 50);
}

[object]
{
    ID: <PRESSUREPLATE1>;
    LOCATION: <ALTERROOM>;
    NAME: "left pressure plate";
    DESC: "It's the pressure plate to your left.%nIf something heavy is placed on top of it, then it may do something.";
    SYNS: "left plate", "first plate", "first pressure plate", "plate 1", "plate", "pressure plate";
    VALUES: (surface: 50);
}

[object]
{
    ID: <PRESSUREPLATE2>;
    LOCATION: <ALTERROOM>;
    NAME: "right pressure plate";
    DESC: "It's the pressure plate to your right.%nIf something heavy is placed on top of it, then it may do something.";
    SYNS: "right plate", "second plate", "second pressure plate", "plate 2", "other plate", "plate", "pressure plate";
    VALUES: (surface: 50);
}

[object]
{
    ID: <WATERCONTAINER1>;
    LOCATION: <PRESSUREPLATE1>;
    LOCATIONTYPE: 1;
    NAME: "clay pot";
    DESC: "It's an old clay pot that was found holding down a pressure plate.%nIt looks like it would be heavy when filled with water.";
    ALTDESC: "It's an old clay pot that was found holding down a pressure plate.%nIt's filled to the brim with water! It's heavy enough to hold down the pressure plate.";
    CONDS: QUERY<selfWeight(watercontainer1,50)>;
    SYNS: "pot", "container", "clay container";
    PROPERTIES: _TAKEABLE;
    VALUES: (volume: 25), (capacity: 25), (weight : 50);
}

[action]
{
    ID: <WATERCONTAINER1>;
    ACTION:
        COND<PRSA: SPILL, EMPTY, DROP, POUR && QUERY<selfWeight(watercontainer1,50)>>
        {
            EFF<SETVAR: <WATERCONTAINER1>: weight, 10>
            EFF<TELL: "The clay pot was emptied of all of its water.">
        }
        COND<PRSA: FILL, PUT && !QUERY<selfWeight(watercontainer1,50)>>
        {
            COND<PRSI: <WATERFALL> && PRSA: UNDER>
            {
                EFF<PLACE: <WATERCONTAINER1>, <WATERFALL>, UNDER>
                EFF<TELL: "You placed the clay pot under the waterfall. It fills up with water and becomes heavy!">
                EFF<SETVAR: <WATERCONTAINER1>: weight, 50>
            }
            COND<PRSI: <WATERFALL>, <RIVER>>
            {
                EFF<TELL: "You filled the clay pot up with water and it became heavy!">
                EFF<SETVAR: <WATERCONTAINER1>: weight, 50>
            }
            COND<INSCOPE: <WATERCONTAINER1>, <HERE> && INSCOPE: <WATERFALL>, <HERE>>
            {
                EFF<TELL: "You filled the clay pot up with water and it became heavy!">
                EFF<SETVAR: <WATERCONTAINER1>: weight, 50>
            }
            COND<INSCOPE: <WATERCONTAINER1>, <HERE> && INSCOPE: <RIVER>, <HERE>>
            {
                EFF<TELL: "You filled the clay pot up with water and it became heavy!">
                EFF<SETVAR: <WATERCONTAINER1>: weight, 50>
            }
        };
}

[room]
{
    ID: <TREASUREROOM>;
    U: <ALTERROOM>;
    NAME: "Treasure Room";
    DESC: "Dazzling golden walls surround the room, the faces of ancient deities etched into each one!";
    DET: "the";
}

[object]
{
    ID: <CHEST>;
    LOCATION: <TREASUREROOM>;
    NAME: "treasure chest";
    DESC: "A golden treasure chest studded with rubies and emeralds.";
    SYNS: "chest", "box";
    PROPERTIES: _TAKEABLE, _CLOSABLECONTAINER, _LOCKED;
    VALUES: (capacity: 50), (volume: 50), (weight: 25);
}

[action]
{
    ID: <CHEST>;
    ACTION:
        COND<PRSA: UNLOCK && ANDPROPERTIES: <CHEST>, _LOCKED && HAVEITEM: <KEY>>
        {
            EFF<REMPROPERTY: <CHEST>, _LOCKED>
            EFF<TELL: "'CLICK' - The golden chest was unlocked!">
        }
        COND<PRSA: LOCK && !ANDPROPERTIES: <CHEST>, _LOCKED && HAVEITEM: <KEY>>
        {
            COND<EQUALS: <CHEST> : isClosed, TRUE>
            {
                EFF<ADDPROPERTY: <CHEST>, _LOCKED>
                EFF<TELL: "You locked the golden chest.">
            }
            COND<EQUALS: <CHEST> : isClosed, FALSE>
            {
                EFF<TELL: "You can't lock the chest when it's open.">
            }
        };
}

[object]
{
    ID: <TREASURE>;
    LOCATION: <CHEST>;
    NAME: "treasure";
    DESC: "All the riches you could ever dream of! Sparkling jewels, shining gold, glowing rubies, and more!";
    PROPERTIES: _TAKEABLE;
    VALUES: (volume: 50), (weight: 25);
}

[room]
{
    ID: <STORAGEROOM>;
    W: <COLUMNS>;
    U: <ATTIC>;
    UCOND: QUERY<climbableStack(3,storageroom)>;
    UBLOCKMESSAGE: "There's nothing tall and stable enough for you to climb on to reach the attic.%nYou've seen some blocks lying around in other rooms, maybe you could reach it if you stack them up...%nThe cupboard against the wall is also stable and can be climbed on.";
    NAME: "Storage Room";
    DESC: "This room looks like it was used to store things. There is all sorts of wood and degraded items lying about.%nA marble cupboard is standing against the wall opposite you, and a slight chill is coming from the rotted hatch to the attic above you.";
}

[object]
{
    ID: <CUPBOARD>;
    LOCATION: <STORAGEROOM>;
    NAME: "cupboard";
    DESC: "An old marble cupboard. It's amazing that the thin stone door still slides open and shut!%nThe cupboard looks big enough to fit things inside.%nWhilst a little dusty, the surface looks stable. Could it be used to climb up to the attic above if we stack some things on top?";
    SYNS: "counter", "compartment", "surface";
    PROPERTIES: _CLOSABLECONTAINER, _STABLE;
    VALUES: (capacity: 50), (surface: 50);
}

[object]
{
    ID: <WATERCONTAINER2>;
    LOCATION: <CUPBOARD>;
    NAME: "copper jug";
    DESC: "It's an old copper jug. It's colouration has slowly turned mint green over time.%nIt looks like it would be heavy when filled with water.";
    ALTDESC: "It's an old copper jug. It's colouration has slowly turned mint green over time.%nIt's filled to the brim with water! It should be heavy enough to hold down one of the pressure plates.";
    CONDS: QUERY<selfWeight(watercontainer2,50)>;
    SYNS: "jug", "container", "copper container";
    PROPERTIES: _TAKEABLE;
    VALUES: (volume: 25), (capacity: 25), (weight: 10);
}

[action]
{
    ID: <WATERCONTAINER2>;
    ACTION:
        COND<PRSA: SPILL, EMPTY, DROP, POUR && QUERY<selfWeight(watercontainer2,50)>>
        {
            EFF<SETVAR: <WATERCONTAINER2>: weight, 10>
            EFF<TELL: "The copper jug was emptied of all of its water.">
        }
        COND<PRSA: FILL, PUT && !QUERY<selfWeight(watercontainer2,50)>>
        {
            COND<PRSI: <WATERFALL> && PRSA: UNDER>
            {
                EFF<PLACE: <WATERCONTAINER2>, <WATERFALL>, UNDER>
                EFF<TELL: "You placed the copper jug under the waterfall. It fills up with water and becomes heavy!">
                EFF<SETVAR: <WATERCONTAINER2>: weight, 50>
            }
            COND<PRSI: <WATERFALL>, <RIVER>>
            {
                EFF<TELL: "You filled the copper jug up with water and it became heavy!">
                EFF<SETVAR: <WATERCONTAINER2>: weight, 50>
            }
            COND<INSCOPE: <WATERCONTAINER2>, <HERE> && INSCOPE: <WATERFALL>, <HERE>>
            {
                EFF<TELL: "You filled the copper jug up with water and it became heavy!">
                EFF<SETVAR: <WATERCONTAINER2>: weight, 50>
            }
            COND<INSCOPE: <WATERCONTAINER2>, <HERE> && INSCOPE: <RIVER>, <HERE>>
            {
                EFF<TELL: "You filled the copper jug up with water and it became heavy!">
                EFF<SETVAR: <WATERCONTAINER2>: weight, 50>
            }
        };
}

[object]
{
    ID: <PLANK>;
    LOCATION: <STORAGEROOM>;
    NAME: "wooden plank";
    DESC: "A long wooden plank that has somehow managed to avoid rotting away.%nIt looks sturdy enough to be used as a bridge.";
    SYNS: "bridge", "wood", "log", "plank";
    PROPERTIES: _TAKEABLE;
    VALUES: (volume: 50), (surface: 50), (weight: 25);
}

[action]
{
    ID: <PLANK>;
    ACTION:
        COND<PRSA: PUT, PLACE && PRSO: <PLANK> && PRSI: <RIVER>>
        {
            EFF<PLACE: <PLANK>, <RIVERSTILTS>, ON>
        }
        COND<PRSA: MAKE, FORM && PRSO: <PLANK> && HERE: <EASTRIVER>>
        {
            EFF<PLACE: <PLANK>, <RIVERSTILTS>, ON>
        };
}

[room]
{
    ID: <ATTIC>;
    D: <STORAGEROOM>;
    NAME: "Attic";
    DESC: "A musty room above the storage room. A few small things are scattered around amongst all of the rubble, dust, and cobwebs.";
}

[object]
{
    ID: <KEY>;
    LOCATION: <ATTIC>;
    NAME: "golden key";
    DESC: "An old ornate golden key. It looks important.";
    SYNS: "key";
    PROPERTIES: _TAKEABLE;
    VALUES: (volume: 10), (weight: 1);
}

[room]
{
    ID: <ROCKYTUNNEL>;
    N: <EASTRIVER>;
    E: <RUINSENTRANCE>;
    NAME: "Rocky Tunnel";
    DESC: "This claustrophobic tunnel is littered with rocks and boulders.";
}

[object]
{
    ID: <MEDIUMBLOCK>;
    LOCATION: <ROCKYTUNNEL>;
    NAME: "medium block";
    DESC: "It's a medium sized block of rock. This boulder looks as if it collapsed from the walls of the rocky tunnel, perhaps it could be used as a building block to climb on.";
    SYNS: "medium block", "medium boulder", "rock block", "boulder", "block", "medium";
    PROPERTIES: _TAKEABLE, _STABLE;
    VALUES: (volume: 50), (surface: 50), (weight: 50);
}

[room]
{
    ID: <EASTRIVER>;
    N: <WATERFALLCAVERN>;
    S: <ROCKYTUNNEL>;
    E: <COLUMNS>;
    W: <WESTRIVER>;
    WCOND: QUERY<isLocated(plank,riverstilts,1)>;
    WBLOCKMESSAGE: "The river is flowing too rapidly to safely cross without some sort of bridge!";
    NAME: "East River Cavern";
    DESC: "The east side of the river. Rotten wooden stilts extend across the river as if there used to be a bridge here in the distant past";
    DET: "the";
}

[object]
{
    ID: <RIVER>;
    LOCATION: <EASTRIVER>;
    NAME: "river";
    DESC: "A fast flowing river snaking it's way through the expansive caverns.%nMost of this cave system would have been forged by this gushing torrent over time.";
    SYNS: "water", "torrent", "rapids";
    VALUES: (capacity: 0);
}

[object]
{
    ID: <RIVERSTILTS>;
    LOCATION: <RIVER>;
    NAME: "stilts";
    DESC: "Rotting wooden stilts that extend across the river. This suggests a bridge existed here once.";
    VALUES: (surface: 50), (volume: 0);
    DET: "some";
    ARE: TRUE;
}

[room]
{
    ID: <WATERFALLCAVERN>;
    S: <EASTRIVER>;
    E: <ALTERROOM>;
    NAME: "Waterfall Cavern";
    DESC: "A roaring waterfall is cascading down into this spacious cavern. A river stretches south from its base.";
}

[object]
{
    ID: <WATERFALL>;
    LOCATION: <WATERFALLCAVERN>;
    NAME: "waterfall";
    DESC: "The sound of the roaring waterfall echoes throughout the cavern. It feeds into the river flowing southward.";
    SYNS: "fall";
    VALUES: (below: 100);
}

[room]
{
    ID: <WESTRIVER>;
    N: <ANCIENTQUARRY>;
    E: <EASTRIVER>;
    ECOND: QUERY<isLocated(plank,riverstilts,1)>;
    EBLOCKMESSAGE: "The river is flowing too rapidly to safely cross without some sort of bridge!";
    NAME: "West River Cavern";
    DESC: "This large cavern is split in two by the fast-flowing river. Massive Stalactites extend down from the roof of the cavern. Hopefully they don't fall...";
    DET: "the";
}

[room]
{
    ID: <ANCIENTQUARRY>;
    S: <WESTRIVER>;
    NAME: "Ancient Quarry";
    DESC: "This seems to be where all of the marble and stone for the underground temple was excavated from. There is still a lot of stone lying about.";
}

[object]
{
    ID: <LARGEBLOCK>;
    LOCATION: <ANCIENTQUARRY>;
    NAME: "large block";
    DESC: "A massive marble block that never ended up leaving the quarry. It's a miracle you're able to even lift this thing!";
    SYNS: "large block", "big block", "biggest block", "big marble block", "large marble block", "block", "large", "big";
    PROPERTIES: _TAKEABLE, _STABLE;
    VALUES: (volume: 75), (surface: 75), (weight: 75);
}

[room]
{
    ID: <DEADROOM>;
    NAME: "Dead Room";
    DESC: "A room that the player shouldn't be able to access.";
}

[end]
{
    CHECK:
        QUERY<climbableStack(3,storageroom)>
        {
            EFF<TELL: "With all three blocks stacked on top of one another, you should be able to climb up to the attic above you!">
        }
        ELSE
        {
            EFF<TELL: "You're now unable to climb up to reach the attic.">
        };
    CHECK:
        QUERY<isLocated(plank,riverstilts,1)>
        {
            EFF<TELL: "Using the plank as a makeshift bridge, you can now safely cross!">
        }
        ELSE
        {
            EFF<TELL: "Without the plank, you can no longer cross the river safely.">
        };
    CHECK:
        QUERY<weightOnObjectAtLeast(pressureplate1,50)>, _DEFAULT
        {
            EFF<TELL: "You hear a rumble as the pressure plate clicks into place.">
        }
        ELSE
        {
            EFF<TELL: "The Pressure plate clicks as it lift back up.">
        };
    CHECK:
        QUERY<weightOnObjectAtLeast(pressureplate2,50)>
        {
            EFF<TELL: "You hear a rumble as the pressure plate clicks into place.">
        }
        ELSE
        {
            EFF<TELL: "The Pressure plate clicks as it lift back up.">
        };
    CHECK:
        QUERY<weightOnObjectAtLeast(pressureplate1,50), weightOnObjectAtLeast(pressureplate2,50)>
        {
            EFF<TELL: "You hear a loud rumbling as the alter before you lowers into the ground and reveals a hidden stairway...">
            EFF<PLACE: <ALTER>, <DEADROOM>, IN>
        }
        ELSE
        {
            EFF<TELL: "The alter rises back up from the floor and hides the secret stairway.">
            EFF<PLACE: <ALTER>, <ALTERROOM>, IN>
        };
    CHECK:
        QUERY<isLocated(treasure,player,0)>
        {
            EFF<TELL: "Congratulations! You obtained the ancient treasure!">
        };
}